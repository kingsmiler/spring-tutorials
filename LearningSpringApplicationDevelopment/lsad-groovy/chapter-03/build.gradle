allprojects {
    apply plugin: 'groovy'
    group 'org.packt.lsad.groovy'
    version '1.0'
}

// 所有子项目的通用配置
subprojects {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    // 不编译Java
    [compileJava, compileTestJava, javadoc]*.enabled = false

    // 避免编译时缺省状态下会因为中文字符而失败
    [compileGroovy, compileTestGroovy]*.options*.encoding = 'UTF-8'

    ext {
        groovyVersion = "2.4.5"
        junitVersion = "4.12"
        springVersion = "4.2.4.RELEASE"
    }

    repositories {
        mavenLocal()
        maven {
            url 'http://localhost:8081/nexus/content/groups/public/'
        }
        maven {
            url "http://repo.spring.io/release"
        }
        mavenCentral()
    }

    jar {
        manifest {
            attributes("Implementation-Title": "Gradle")
        }
    }

    configurations {
        // 所有需要忽略的包定义在此
        all*.exclude group: 'commons-httpclient'
    }

    dependencies {
        // 通用依赖
        compile(
                "org.codehaus.groovy:groovy-all:$groovyVersion",
                "org.springframework:spring-context:$springVersion"
        )

        // 依赖mave中不存在的jar
        ext.jarTree = fileTree(dir: 'libs', include: '**/*.jar')
        ext.rootProjectLibs = new File(rootProject.rootDir, 'libs').getAbsolutePath()
        ext.jarTree += fileTree(dir: rootProjectLibs, include: '**/*.jar')
        compile jarTree

        // 测试依赖
        testCompile(
                "org.springframework:spring-test:$springVersion",
                "junit:junit:$junitVersion"
        )
    }
}
